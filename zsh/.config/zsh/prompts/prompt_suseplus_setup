# I rather like the Suse prompt bundled with zsh. Making the prompt a scp(1)
# compatible remote path is quite clever. This prompt offers some cosmetic
# improvements, namely:
#   Color prompt
#   Red, angry prompt if running as root
#   Version control status when in a VC'd directory
#   Display non-zero exit codes
# The prompt sticks to ASCII characters and ANSI text effects for compatability
# reasons.

prompt_suseplus_precmd() {
  vcs_info
}

prompt_suseplus_setup () {
  prompt_opts=(cr percent subst)

  autoload -Uz vcs_info
  zstyle ':vcs_info:*' enable git hg svn
  zstyle ':vcs_info:*' check-for-changes true
  #zstyle ':vcs_info:*' formats TODO
  #zstyle ':vcs_info:*' actionformats TODO
  add-zsh-hook precmd prompt_suseplus_precmd

  local user_inner="%B%n%b"
  local user="%(!.%F{red}%U.%F{blue})$user_inner%(!.%u.)%f"
  local host_inner="%B%m%b"
  local host="%F{blue}@$host_inner%f"
  local dir_inner="%B%~/%b"
  local dir="%F{cyan}:$dir_inner%f"
  local vcs="\$vcs_info_msg_0_"
  local err="%(?.. %F{red}[%?]%f)"
  #local sep_inner="%B>%b"
  local sep_inner="%B%(!.>>>.>)%b"
  local sep1=" %(?.%F{blue}.%F{red})$sep_inner%f"
  local sep2=" %F{blue}$sep_inner%f"

  PS1="$user$host$dir$vcs$err$sep1 "
  PS2="$sep2 "
}

prompt_suseplus_setup "$@"
